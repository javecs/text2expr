buildscript {
    ext.kotlin_version = '1.1.1'
    ext.antlr_version = '4.7'
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group 'xyz.javecs.tools'
version '0.0.7'

apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

repositories {
    maven {
        url  "http://dl.bintray.com/javecs/tools"
    }
}

dependencies {
    antlr "org.antlr:antlr4:$antlr_version"
    compile "xyz.javecs.tools:expr:0.2.3"
    compile "com.google.guava:guava:21.0"
    compile "com.atilika.kuromoji:kuromoji-ipadic:0.9.0"
    compile "org.reflections:reflections:0.9.11"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

generateGrammarSource {
    outputDirectory = file('src/main/java/xyz/javecs/tools/text2expr/parser')
    arguments = ['-no-listener', '-visitor', '-package', 'xyz.javecs.tools.text2expr.parser']
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "javecs"
            name "MUNI"
            email "moonhee.javecs@gmail.com"
        }
    }
}

publishing {
    publications {
        ExprPublication(MavenPublication) {
            from components.java
            Artifact sourcesJar
            Artifact javadocJar
            groupId 'xyz.javecs.tools'
            artifactId 'text2expr'
            version = project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Javecs Tools Text 2 Expr')
                root.appendNode('name', 'text2expr')
                root.appendNode('url', 'https://github.com/javecs/text2expr')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['ExprPublication']
    configurations = ['archives']
    pkg {
        repo = 'tools'
        name = 'text2expr'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/javecs/expr.git'
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}